#set term png size 1920,1080
#set output "boot-time.png"

set key right top

stats "data.log" u 1:2 nooutput

set xrange [0:STATS_max_x]
set yrange [0:STATS_max_y+50]

set label 1 gprintf("Maximum boot = %g ms", STATS_max_y) at STATS_pos_max_y, STATS_max_y+18 front
set arrow from STATS_pos_max_y, STATS_max_y+15 to STATS_pos_max_y, STATS_max_y lc rgb 'black'

set label 2 gprintf("Minimum boot = %g ms", STATS_min_y) at STATS_pos_min_y-2, STATS_min_y-30 front
set arrow from STATS_pos_min_y, STATS_min_y-30 to STATS_pos_min_y, STATS_min_y lc rgb 'black'

BOOT_max_y=STATS_max_y
BOOT_min_y=STATS_min_y

stats "data.log" u 1:3 nooutput

set label 5 gprintf("Maximum resume = %g ms", STATS_max_y) at STATS_pos_max_y, STATS_max_y+32 front
set arrow from STATS_pos_max_y, STATS_max_y to STATS_pos_max_y, STATS_max_y+30 lc rgb 'black'

set label 6 gprintf("Minimum resume = %g ms", STATS_min_y) at STATS_pos_min_y-2, STATS_min_y+22 front
set arrow from STATS_pos_min_y, STATS_min_y to STATS_pos_min_y, STATS_min_y+20 lc rgb 'black'

plot "data.log" u 1:2 title 'Guest Boot' w p lc rgb 'orange' pt 14 ps 1, \
    "data.log" u 1:3 title 'Snapshot Resume' w p lc rgb 'green' pt 14 ps 1, \
    BOOT_min_y w l lc rgb 'blue' notitle, \
    BOOT_max_y w l lc rgb 'blue' notitle, \
    STATS_min_y w l lc rgb 'purple' notitle, \
    STATS_max_y w l lc rgb 'purple' notitle

